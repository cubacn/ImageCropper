
buildscript {
    ext.cubaVersion = '7.1.2'
    ext.aliRepo=[
            snapshot: 'https://repo.rdc.aliyun.com/repository/72077-snapshot-057BdJ/',
            release: 'https://repo.rdc.aliyun.com/repository/72077-release-FWXfLw/',
            username :rootProject.hasProperty("skdAaliRepo.UserName")?rootProject["skdAaliRepo.UserName"]:"",
            password :rootProject.hasProperty("skdAaliRepo.Password")?rootProject["skdAaliRepo.Password"]:""
    ]
    repositories {

        maven {
            url 'https://dl.bintray.com/cuba-platform/main'

        }
        jcenter()


        maven {
            url 'https://maven.aliyun.com/nexus/content/groups/public/'
        }

        maven {
            credentials {
                username aliRepo.username
                password aliRepo.password
            }
            url aliRepo.release
        }
        maven {
            credentials {
                username aliRepo.username
                password aliRepo.password
            }
            url aliRepo.snapshot
        }

    }
    dependencies {
        classpath "com.haulmont.gradle:cuba-plugin:$cubaVersion"
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.0"
    }
}

def modulePrefix = 'cubacnimgcrop'

def globalModule = project(":${modulePrefix}-global")
def coreModule = project(":${modulePrefix}-core")
def webModule = project(":${modulePrefix}-web")

def servletApi = 'javax.servlet:javax.servlet-api:3.1.0'

apply(plugin: 'cuba')

cuba {
    artifact {
        group = 'cubacn.cmp.crop'
        version = '1.0.0'
        isSnapshot = false
    }

    uploadRepository {
        url =aliRepo.snapshot
        user = aliRepo.username
        password =aliRepo.password
    }

    tomcat {
        dir = "$project.rootDir/deploy/tomcat"
        port = '8086'
        ajpPort = '8014'
        shutdownPort = '8011'
        debugPort = '8792'
    }
}

dependencies {
    appComponent("com.haulmont.cuba:cuba-global:$cubaVersion")
}

def hsql = 'org.hsqldb:hsqldb:2.4.1'

configure([globalModule, coreModule, webModule]) {
    apply(plugin: 'java')
    apply(plugin: 'maven')
    apply(plugin: 'cuba')

    dependencies {
        testCompile('junit:junit:4.12')
    }

    task sourceJar(type: Jar) {
        from file('src')
        classifier = 'sources'
    }

    artifacts {
        archives sourceJar
    }
}

configure([globalModule, coreModule, webModule]) {
    apply(plugin: 'groovy')

    sourceSets {
        main { groovy { srcDirs = ["src"] } }
        test { groovy { srcDirs = ["test"] } }
    }

    sourceSets.main.output.classesDir = new File(project.buildDir, "classes/main")
    sourceSets.test.output.classesDir = new File(project.buildDir, "classes/test")

}

configure(globalModule) {
    dependencies {
        if (!JavaVersion.current().isJava8()) {
            runtime('javax.xml.bind:jaxb-api:2.3.1')
            runtime('org.glassfish.jaxb:jaxb-runtime:2.3.1')
        }
    }

    entitiesEnhancing {
        main {
            enabled = true
        }
    }

    jar {
        manifest {
            attributes('App-Component-Id': cuba.artifact.group)
            attributes('App-Component-Version': cuba.artifact.version + (cuba.artifact.isSnapshot ? '-SNAPSHOT' : ''))
        }
    }
}

configure(coreModule) {

    configurations {
        jdbc
        dbscripts
    }

    dependencies {
        compile(globalModule)
        compileOnly(servletApi)
        jdbc(hsql)
        testRuntime(hsql)
    }

    task cleanConf(description: 'Cleans up conf directory') {
        doLast {
            def dir = new File(cuba.tomcat.dir, "/conf/${modulePrefix}-core")
            if (dir.isDirectory()) {
                ant.delete(includeemptydirs: true) {
                    fileset(dir: dir, includes: '**/*', excludes: 'local.app.properties')
                }
            }
        }
    }

    task deploy(dependsOn: [assemble, cleanConf], type: CubaDeployment) {
        appName = "${modulePrefix}-core"
        appJars(modulePrefix + '-global', modulePrefix + '-core')
    }

    task createDb(dependsOn: assembleDbScripts, description: 'Creates local database', type: CubaDbCreation) {
        dbms = 'hsql'
        host = 'localhost:9010'
        dbName = 'skd'
        dbUser = 'sa'
        dbPassword = ''
    }

    task updateDb(dependsOn: assembleDbScripts, description: 'Updates local database', type: CubaDbUpdate) {
        dbms = 'hsql'
        host = 'localhost:9010'
        dbName = 'skd'
        dbUser = 'sa'
        dbPassword = ''
    }
}

configure(webModule) {
    configurations {
        webcontent
    }

    dependencies {
        compile 'org.webjars.bower:jrcarousel:1.0.0'
        compileOnly(servletApi)
        compile(globalModule)
    }


    task webArchive(type: Zip) {
        from file("$buildDir/web")
        from file('web')
        classifier = 'web'
    }

    artifacts {
        archives webArchive
    }

    task deployConf(type: Copy) {
        from file('src')
        include "cubacn/cmp/crop/**"
        into "$cuba.tomcat.dir/conf/${modulePrefix}"
    }

    task clearMessagesCache(type: CubaClearMessagesCache) {
        appName = "${modulePrefix}"
    }
    deployConf.dependsOn clearMessagesCache

    task cleanConf(description: 'Cleans up conf directory') {
        doLast {
            def dir = new File(cuba.tomcat.dir, "/conf/${modulePrefix}")
            if (dir.isDirectory()) {
                ant.delete(includeemptydirs: true) {
                    fileset(dir: dir, includes: '**/*', excludes: 'local.app.properties')
                }
            }
        }
    }

    task deploy(dependsOn: [assemble, cleanConf], type: CubaDeployment) {
        appName = "${modulePrefix}"
        appJars(modulePrefix + '-global', modulePrefix + '-web')
    }
}


task undeploy(type: Delete, dependsOn: ":${modulePrefix}-web:cleanConf") {
    delete("$cuba.tomcat.dir/shared")
    delete("$cuba.tomcat.dir/webapps/${modulePrefix}-core")
    delete("$cuba.tomcat.dir/webapps/${modulePrefix}")
}

task restart(dependsOn: ['stop', ":${modulePrefix}-core:deploy", ":${modulePrefix}-web:deploy"], description: 'Redeploys applications and restarts local Tomcat') {
    doLast {
        ant.waitfor(maxwait: 6, maxwaitunit: 'second', checkevery: 2, checkeveryunit: 'second') {
            not {
                socket(server: 'localhost', port: '8787')
            }
        }
        start.execute()
    }
}


subprojects {
    apply plugin: 'com.jfrog.bintray'

    bintray {
        user = project.hasProperty('BINTRAY_CUBACN_USER') ? project.property('BINTRAY_CUBACN_USER') : System.getenv('BINTRAY_CUBACN_USER')
        key = project.hasProperty('BINTRAY_CUBACN_API_KEY') ? project.property('BINTRAY_CUBACN_API_KEY') : System.getenv('BINTRAY_CUBACN_API_KEY')

        configurations = ['archives']

        // make files public ?
        publish = true
        // override existing artifacts?
        override = true

        // metadata
        pkg {
            repo = 'addons'           // your repository name
            name = 'ImageCropper'  // package name - it will be created upon upload
            desc = 'A component for cropping big image in CUBA application'  // optional package description

            // organization name, if your repository is created inside an organization.
            // remove this parameter if you don't have an organization
//            userOrg = 'jupiter-org'

            websiteUrl = 'https://github.com/cubacn/ImageCropper'
            issueTrackerUrl = 'https://github.com/cubacn/ImageCropper/issues'
            vcsUrl = 'git@github.com:cubacn/ImageCropper.git' // mandatory for Open Source projects

            licenses = ["Apache-2.0"]
            labels = ['cuba', 'cuba-platform', 'cubaChina', 'imageCropper','Chinese']

            githubRepo = 'cubacn/ImageCropper' // optional Github repository
            githubReleaseNotesFile = 'README.md' // optional Github readme file
        }
    }
}